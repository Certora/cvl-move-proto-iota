#[allow(unused_function, unused_mut_parameter)]
module certora::iota_vec_map_summaries;

use cvlm::ghost::{ ghost_read, ghost_write, ghost_destroy };
use cvlm::manifest::{ shadow, summary };
use cvlm::nondet::nondet;
use iota::vec_map::VecMap;

fun cvlm_manifest() {    
    shadow(b"shadow_vec_map");
    shadow(b"shadow_entry");
    summary(b"size", @iota, b"vec_map", b"size");
    summary(b"empty", @iota, b"vec_map", b"empty");
    summary(b"insert", @iota, b"vec_map", b"insert");
    summary(b"remove", @iota, b"vec_map", b"remove");
    summary(b"get_mut", @iota, b"vec_map", b"get_mut");
    summary(b"get", @iota, b"vec_map", b"get");
    summary(b"try_get", @iota, b"vec_map", b"try_get");
    summary(b"get_entry_by_idx", @iota, b"vec_map", b"get_entry_by_idx");
    summary(b"get_entry_by_idx_mut", @iota, b"vec_map", b"get_entry_by_idx_mut");
    summary(b"contains", @iota, b"vec_map", b"contains");
    summary(b"keys", @iota, b"vec_map", b"keys");
    summary(b"into_keys_values", @iota, b"vec_map", b"into_keys_values");
}

public struct ShadowVecMap<K: copy, V> has copy, drop, store {
    size: u64,
    contents: ShadowContents<K, V>,
    indexed: vector<K>,
}
public native struct ShadowContents<K: copy, V> has copy, drop, store;
public struct ShadowEntry<V> has copy, drop, store {
    value: V,
    present: bool,
    index: u64
}

// #[shadow]
native fun shadow_vec_map<K: copy, V>(map: &VecMap<K, V>): &mut ShadowVecMap<K, V>;

// #[shadow]
native fun shadow_entry<K: copy, V>(contents: &ShadowContents<K, V>, key: K): &mut ShadowEntry<V>;

// #[summary(iota::vec_map::size)]
fun size<K: copy, V>(self: &VecMap<K, V>): u64 {
    shadow_vec_map(self).size
}

public fun empty<K: copy, V>(): VecMap<K, V> {
    let map = nondet<VecMap<K, V>>();
    let shadow = shadow_vec_map(&map);
    shadow.size = 0;
    map
}

// #[summary(iota::vec_map::insert)]
fun insert<K: copy, V>(self: &mut VecMap<K, V>, key: K, value: V) {
    let map = shadow_vec_map(self);
    let entry = shadow_entry(&map.contents, key);
    assert!(!entry.present);
    ghost_write(&mut entry.value, value);
    entry.present = true;
    entry.index = map.size;
    map.size = map.size + 1;
    map.indexed.push_back(key);
}

// #[summary(iota::vec_map::remove)]
fun remove<K: copy, V>(self: &mut VecMap<K, V>, key: &K): (K, V) {
    let map = shadow_vec_map(self);
    let entry = shadow_entry(&map.contents, *key);
    assert!(entry.present);
    entry.present = false;    
    map.size = map.size - 1;
    if (entry.index == map.size) {
        ghost_destroy(map.indexed.pop_back());
    } else {
        ghost_write(&mut map.indexed, nondet());
    };
    (*key, ghost_read(&entry.value))
}

// #[summary(iota::vec_map::get_mut)]
fun get_mut<K: copy, V>(self: &mut VecMap<K, V>, key: &K): &mut V {
    let map = shadow_vec_map(self);
    let entry = shadow_entry(&map.contents, *key);
    assert!(entry.present);
    &mut entry.value
}

// #[summary(iota::vec_map::get)]
public fun get<K: copy, V>(self: &VecMap<K, V>, key: &K): &V {
    let map = shadow_vec_map(self);
    let entry = shadow_entry(&map.contents, *key);
    assert!(entry.present);
    &entry.value
}

// #[summary(iota::vec_map::try_get)]
fun try_get<K: copy, V: copy>(self: &VecMap<K, V>, key: &K): Option<V> {
    let map = shadow_vec_map(self);
    let entry = shadow_entry(&map.contents, *key);
    certora::std_option_summaries::maybe_some(entry.present, ghost_read(&entry.value))
}

// #[summary(iota::vec_map::get_entry_by_idx)]
fun get_entry_by_idx<K: copy, V>(self: &VecMap<K, V>, idx: u64): (&K, &V) {
    let map = shadow_vec_map(self);
    assert!(idx < map.size);
    let key = &map.indexed[idx];
    let entry = shadow_entry(&map.contents, *key);
    (key, &entry.value)
}

// #[summary(iota::vec_map::get_entry_by_idx_mut)]
fun get_entry_by_idx_mut<K: copy, V>(self: &mut VecMap<K, V>, idx: u64): (&K, &mut V) {
    let map = shadow_vec_map(self);
    assert!(idx < map.size);
    let key = &map.indexed[idx];
    let entry = shadow_entry(&map.contents, *key);
    (key, &mut entry.value)
}

// #[summary(iota::vec_map::contains)]
fun contains<K: copy, V>(self: &VecMap<K, V>, key: &K): bool {
    let map = shadow_vec_map(self);
    let entry = shadow_entry(&map.contents, *key);
    entry.present
}

// #[summary(iota::vec_map::keys)]
fun keys<K: copy, V>(self: &VecMap<K, V>): vector<K> {
    shadow_vec_map(self).indexed
}

// #[summary(iota::vec_map::into_keys_values)]
fun into_keys_values<K: copy, V>(self: VecMap<K, V>): (vector<K>, vector<V>) {
    let map = shadow_vec_map(&self);
    let keys = map.indexed;
    ghost_destroy(self);
    (keys, nondet())
}